# Ethernaut Level 15 (GateKeeper2) Walkthrough

The following is a semantic solution walkthrough of the Ethernaut CTF level 15. The aim is to give a readable explanation of the concepts involved, and present a step-by-step walkthrough of the solution. In addition, tangential explanations will also be given where essential.

The contract in question is the following:

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.6.0;

contract GatekeeperTwo {

  address public entrant;

  modifier gateOne() {
    require(msg.sender != tx.origin);
    _;
  }

  modifier gateTwo() {
    uint x;
    assembly { x := extcodesize(caller()) }
    require(x == 0);
    _;
  }

  modifier gateThree(bytes8 _gateKey) {
    require(uint64(bytes8(keccak256(abi.encodePacked(msg.sender)))) ^ uint64(_gateKey) == uint64(0) - 1);
    _;
  }

  function enter(bytes8 _gateKey) public gateOne gateTwo gateThree(_gateKey) returns (bool) {
    entrant = tx.origin;
    return true;
  }
}
```

Gate1:

Passing Gate1 is easy. We have already done this in Gatekeeper1. Simply, instead of making the call to enter directly, call it via a contract.

Gate2:

Let's understand the requirement here. It requires that the `codesize` of the calling account needs to be 0. the opcode `codesize()` gives the size in bytes of the code (if any) of the account at the given address. This gate is also easy to pass. Know that `codesize()` will return a size of 0 even if the caller is an account but has no code other than inside the constructor. Thus, we just need to have make our call in our attack contract's constructor.

Gate3:

This is an interesting gate.

`require(uint64(bytes8(keccak256(abi.encodePacked(msg.sender)))) ^ uint64(_gateKey) == uint64(0) - 1);`

First, let's see what the right side of the require is since its shorter: `uint64(0) - 1)` Simply, 1 is being deducted from a 8byte uint with a value of 0. Now, this is solidity 0.6 so there is no inherent under/over flow check. As such, going 1 below 0 in an 8 byte container will result in the max value storable by uint64/ 8bytes, which is    `18446744073709551615` or `0xffffffffffffffff` in bytes. So, the left side must be equal to `0xffffffffffffffff` . With this information, the require statement looks like:

`require(uint64(bytes8(keccak256(abi.encodePacked(msg.sender)))) ^ uint64(_gateKey) == uint64(0xffffffffffffffff);`

Thus, the left side must resolve to 0xffffffffffffffff if the check is to pass.

Now, let's look at the left side of the require.

It appears to first convert `msg.sender` address to a packed encoding, then calculate its 32 byte `keccak256` hash, then cast it down to 8 bytes (remember, when casting down in bytesN, the left most bytes are dropped to fit the size requirement - here, left most 24 bytes will be dropped), and then finally, cast it into uint64 (this doesn't do anything since uint64 is equal in size to bytes 8).  Then, this value is XOR'd with `uint64(_gateKey)`, which will give a result that is equal to `0xffffffffffffffff`.

msg.sender will need to be the address of our attack contract, since we need to pass gate1. This poses a problem, the call to enter will be made through the constructor of our attack contract (This is to meet gate2 requirement of zero codesize). Thus, we cannot deploy it and then use its address to find the key. The key will thus have to be calculated in real time inside the constructor.

At this point, let's assume that instead of `msg.sender`, `tx.origin` was instead used in require3. In that case the process would be as follows:

For me, tx.origin was `0xecA5bA71FC055AF7FCdFb5755C575B67Ac48EcDF`), Using our helper contract, we will calculate: `uint64(bytes8(keccak256(abi.encodePacked(msg.sender)))` . This results in: `6427117074688828612` or `0x5931b4ed56ace4c4` in bytes. Let us plug these values in the require statement and try to understand it better:

`require(uint64(0x5931b4ed56ace4c4) ^ uint64(_gateKey) == uint64(0xffffffffffffffff);`

It is starting to become clear. When some operation denoted by ^ is done between the 8 bytes `0x5931b4ed56ace4c4` and the 8 byte `gateKey`, the result is equal to `0xffffffffffffffff` i.e., the max bytes8/ uint64 value.

The operation in question is XOR. Put simply, whenever we XOR two bits, the resulting bit is only true of the two bits are different. For example: `1010 ^ 0110 = 1100`. The operation of using a set of bits, the key in this case, to XOR another set (`0x5931b4ed56ace4c4` in this case) is called **bit-masking**. We apply a mask over the value to convert it to a desired form, in this case (`0xffffffffffffffff`). `0xff` this represents a fully turned on/ max value byte since `f` is the highest value in hex.

We now need to find an 8byte mask that turns on all the bits in `0x5931b4ed56ace4c4`, which will make it 0xffffffffffffffff. This will be our key. For this, let's first convert the byte value we want to mask, into binary. For conversion we will use this [website](https://www.rapidtables.com/convert/number/binary-to-hex.html). After conversion we get:

`0101 1001 0011 0001 1011 0100 1110 1101 0101 0110 1010 1100 1110 0100 1100 0100`

Now, we will have to write a binary value (the mask) of the same size, but with each corresponding bit different. This is so that when this new binary value is XOR'd with the above binary value, the resulting value has all bits turned on. The **mask** is thus as follows.

`1010 0110 1100 1110 0100 1011 0001 0010 1010 1001 0101 0011 0001 1011 0011 1011`

This mask, when XOR'd with our original value, will turn all the bits on, and we will get the highest possible 8byte value.

`1111 1111 1111 1111 1111 1111 1111 1111 1111 1111 1111 1111 1111 1111 1111 1111`

which is equal to `0xffffffff` in hex. Let's convert the mask from binary to hex since we the values we are dealing with are in hex. After conversion, we get `0xA6CE4B12A9531B3B`. We can use our helper contract to perform the XOR operation: `0x5931b4ed56ace4c4 ^ 0xA6CE4B12A9531B3B` It gives us `0xffffffffffffffff`, which is equal to what is on the right side of the require check. We have found our key!

Alternatively, we could also have found the mask without converting `0x5931b4ed56ace4c4` to binary first. Essentially, we need to turn on all the bits. That is, put in the max possible value in a byte. The max possible value in hex is f, and so in a byte, the max possible storable value is 0xff. Lefts look at the first of the 8 bytes in our value: `0x 59 31 b4 ed 56 ac e4 c4` the value stored in the first byte is 59. What mask can we apply in hex to make it ff? For this, we find the difference between the max possible value in a byte, ff in this case, and our value, 59. f in hex is equal to 15. 15-9 is 6. 15 - 5 is 10, which is A in hex. Thus, the result is 0xa6. If we XOR 0xa6 with 0x59: `0x59 ^ 0xa6`, the resulting value will be `0xff` as all the inactive bits will be turned on. We can repeat this calculation with the remaining 7 bytes and find the mask this way.

We can also use this hex [calculator](https://www.calculator.net/hex-calculator.html?number1=31&c2op=-&number2=ff&calctype=op&x=92&y=30) to deduct 0x59 from 0xff (or perform any hex arithmetic).

The above explanation using tx.origin was to teach ourselves important concepts and solidify our understanding of bits, bytes and the XOR operation.

Let us return to solving require3.

`require(uint64(bytes8(keccak256(abi.encodePacked(msg.sender)))) ^ uint64(_gateKey) == uint64(0) - 1);`

Firstly, to simplify, we can write it as an expression:
`A ^ x = C` A and C are constant values, while x is our mask. If we XOR A with x, we get C. Similarly, if we XOR A with C, we will get our bit mask, x. This way, we can calculate the key right in the constructor of our attack contract. So, we can make x (the key) the subject of the require expression:

`require(uint64(bytes8(keccak256(abi.encodePacked(msg.sender)))) ^ uint64(0) - 1) == uint64(_gateKey);`

Uint64 has the same size as bytes8, so we can drop the uint64 cast on the first value. uint64(0)-1 is the largest 8 byte value so it's 0xffffffffffffffff.

`require(bytes8(keccak256(abi.encodePacked(msg.sender))) ^ bytes8(uint64(0) - 1) == bytes8(_gateKey);`

The above is our equation to find the _gateKey.

Consider the code below:

```solidity
contract Attack {
// Pass in the address of your gatekeeper 2 instance when deploying
constructor(address _gateKeeper2) public payable {
// Find the key (which is the bit-mask) using our equation
    bytes8 _key = bytes8(keccak256(abi.encodePacked(address(this)))) ^ bytes8(uint64(0)-1);
    // Make the low level call to the target contract
    (bool result, bytes memory data) = _gateKeeper2
           .call(abi.encodeWithSignature("enter(bytes8)", _key));
           require(result);
}
```

Finally, just deploy the contract with the instance address, and become the mighty entrant!

Left and right shifts are important when multiplying or dividing by powers of 2.